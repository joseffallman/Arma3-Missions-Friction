/*
 * Name:	AirStationAssault
 * Date:	2020-06-24
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * A mission where players assault and defend Air Station Mike.
 * The ACTING side will be the side attacking, and the OPPOSING side will be the side defending.
 */

using Tvtcf.Common;
using Tvtcf.Server;
using Campaigns;

namespace Campaigns.ShoeLace
{
	public class AirStationAssault : ShoeLaceMission
	{
		private fields ["_mPrimaryTargetVehicle" as Object, "_mSecondaryTargetVehicle" as Object, "_mAllVehicles" as Array, "_mAllOpposingUnits" as Array /* of Object */];
		private fields ["_mReinforcementsAlerted" as Boolean, "_mProwler" as Object, "_mReinforcementsArrived" as Boolean, "_mReinforcementGroup" as Group];
		
		// Creates a AirStationAssault object.
		public constructor()
		{
			call _base.Constructor;
			_mAllOpposingUnits = [];
			_mReinforcementsAlerted = false;
			_mReinforcementsArrived = false;
			_mProwler = objNull;
			_mReinforcementGroup = grpNull;
		};
		
		protected override method InitMission("_mission" as MissionSettings)
		{
			_mission.Name = "Air Station Assault";
			_mission.StartTimeOfDay = floor random 4;
			_mission.StartMarkersActing = ["m01a_startarea1", "m01a_startarea2", "m01a_startarea3"];
			_mission.StartMarkersOpposing = ["m01o_startarea1"];
			_mission.SideMarkersActing = ["m01a_PrimaryTarget"];
			_mission.AddReturnObjectiveForActing = true;
		};
		
		protected override method String AddBriefingInfoForActing()
		{
			var _p1 = "A lot of enemy supply trucks have been spotted entering Air Station Mike lately. Our enemies are obviously up to something. Let's hit while we have a good chance of make some damage.";
			var _p2 = "<br /><br />Important! Keep this operation clean. When the first thing blows, or when the first enemy is killed, they will have reinforcements all over the place.";
			var _p3 = "<br /><br />Remember to bring explosives!";
			var _p4 = "";
			
			if ("DRONE" in _self.GainedTokensActing) then {
				_p4 = "<br /><br />Earlier success has given you access to a UAV of type Darter.";
			};
			
			return _p1 + _p2 + _p3 + _p4 + call _base.AddBriefingInfoForActing;
		};
		
		protected override method String AddBriefingInfoForOpposing()
		{
			var _p1 = "We have tried to keep this operation as secret as we can. If we keep Air Station Mike safe until tomorrow, we are in a good position to soon advance into enemy territory.";
			var _p2 = "<br /><br />Friendly forces are on high alert. If you or any equipment are attacked then expect immediate arrival of reinforcements.";
			var _p3 = "";
			
			if ("DRONE" in _self.GainedTokensOpposing) then {
				_p3 = "<br /><br />Earlier success has given you access to a UAV of type Darter.";
			};
			
			return _p1 + _p2 + _p3 + call _base.AddBriefingInfoForOpposing;
		};
		
		protected override method Setup()
		{
			private ["_targetVehicles" as Array, "_vehicleMarkers" as Array, "_vehicle" as Object];
			
			_self.OpposingOrdnanceBox addBackpackCargoGlobal ["B_Kitbag_cbr", 1];
			
			// Spawn opposing side's objects
			
			_mAllVehicles = [];
			_targetVehicles = [];
			_vehicleMarkers = ["m01o_Vehicle1", "m01o_Vehicle2", "m01o_Vehicle3", "m01o_Vehicle4", "m01o_Vehicle5", "m01o_Vehicle6", "m01o_Vehicle7"];
			
			[_vehicleMarkers] call ShoeLaceMission.ShuffleArray;
			
			_vehicle = ["AMMO TRUCK", _vehicleMarkers select 0, MissionSide.Opposing] call _self.CreateSideVehicleOnMarker;
			_vehicle setVariable ["ObjectiveText", "Destroy ammo truck"];
			_vehicle lock true;
			_targetVehicles pushBack _vehicle;
			_mAllVehicles pushBack _vehicle;
			
			_vehicle = ["DEVICE TRUCK", _vehicleMarkers select 1, MissionSide.Opposing] call _self.CreateSideVehicleOnMarker;
			_vehicle setVariable ["ObjectiveText", "Destroy device truck"];
			_vehicle lock true;
			_targetVehicles pushBack _vehicle;
			_mAllVehicles pushBack _vehicle;
			
			_vehicle = ["REPAIR TRUCK", _vehicleMarkers select 2, MissionSide.Opposing] call _self.CreateSideVehicleOnMarker;
			_vehicle setVariable ["ObjectiveText", "Destroy repair truck"];
			_vehicle lock true;
			_targetVehicles pushBack _vehicle;
			_mAllVehicles pushBack _vehicle;
			
			_vehicle = ["MEDICAL TRUCK", _vehicleMarkers select 3, MissionSide.Opposing] call _self.CreateSideVehicleOnMarker;
			_vehicle lock true;
			_mAllVehicles pushBack _vehicle;
			
			_vehicle = ["MRAP", _vehicleMarkers select 4, MissionSide.Opposing] call _self.CreateSideVehicleOnMarker;
			_mAllVehicles pushBack _vehicle;
			
			_vehicle = ["QUADBIKE", _vehicleMarkers select 5, MissionSide.Opposing] call _self.CreateSideVehicleOnMarker;
			_mAllVehicles pushBack _vehicle;
			
			[_targetVehicles] call ShoeLaceMission.ShuffleArray;

			_mPrimaryTargetVehicle = _targetVehicles select 0;
			_mSecondaryTargetVehicle = _targetVehicles select 1;

			// Add an AI group defending the area.
			
			private _guard = ["RIFLEMAN", getMarkerPos "m01o_startarea1", MissionSide.Opposing] call _self.CreateSideAiSoldier;
			private _guardGroup = group _guard;
			["RIFLEMAN", getMarkerPos "m01o_startarea1", MissionSide.Opposing, _guardGroup] call _self.CreateSideAiSoldier;
			[_guardGroup, "m01o_startarea1"] execVM "Engima\SearchPatrol\SearchPatrol.sqf";
			
			// Add eventhandlers to AI
			
			{
				_mAllOpposingUnits pushBack _x;
				
				_x setVariable ["UnitSide", side group _x];
				_x addEventHandler ["Killed", {
					params ["_unit" as Object, "_killer" as Object, "_instigator" as Object, "_useEffects" as Boolean];
					_unit setVariable ["KilledBySide", side group _killer];
				}];
			} foreach units _guardGroup as Object;
		
			call _base.Setup;
		
			// Spawn acting side's base objects
			
			if (isNull _self.ActingOrdnanceBox) then {
				_self.ActingOrdnanceBox = [MissionSide.Acting] call _self.CreateExplosivesBoxInBase;
			};
			
			// If the acting side has a drone, put it in the explosives box.
			
			if ("DRONE" in _self.GainedTokensActing) then {
				var _actualSide = [MissionSide.Acting] call _self.GetActualSide;
				var _uavBackpackClassName = ["UAV BACKPACK", _actualSide] call _self.ObjectTypeDictionary.GetVehicleType;
				var _uavTerminalClassName = ["UAV TERMINAL", _actualSide] call _self.ObjectTypeDictionary.GetVehicleType;
				_self.ActingOrdnanceBox addBackpackCargoGlobal [_uavBackpackClassName, 1];
				_self.ActingOrdnanceBox addItemCargoGlobal [_uavTerminalClassName, 1];
			};
			
			// Spawn opposing side's base objects
			
			if ("DRONE" in _self.GainedTokensOpposing) then
			{
				if (isNull _self.OpposingOrdnanceBox) then
				{
					_self.OpposingOrdnanceBox = ["AMMOBOX ORDNANCE", BaseMarker.AmmoCrate, MissionSide.Opposing] call _self.CreateSideVehicleInBase;
					[_self.OpposingOrdnanceBox] call _self.MakeEmptyAmmoBox;
				};
				
				var _actualSide = [MissionSide.Opposing] call _self.GetActualSide;
				var _uavBackpackClassName = ["UAV BACKPACK", _actualSide] call _self.ObjectTypeDictionary.GetVehicleType;
				var _uavTerminalClassName = ["UAV TERMINAL", _actualSide] call _self.ObjectTypeDictionary.GetVehicleType;
				_self.OpposingOrdnanceBox addBackpackCargoGlobal [_uavBackpackClassName];
				_self.OpposingOrdnanceBox addItemCargoGlobal [_uavTerminalClassName, 1];
			};
		};
		
		protected override method OnOpposingGroupInserted("_e" as InsertionEventArgs)
		{
			// Add eventhandlers to player group
			
			{
				_mAllOpposingUnits pushBack _x;
				
				_x setVariable ["UnitSide", side group _x];
				_x addEventHandler ["Killed", {
					params ["_unit" as Object, "_killer" as Object, "_instigator" as Object, "_useEffects" as Boolean];
					_unit setVariable ["KilledBySide", side group _killer];
				}];
			} foreach _self.OpposingUnits as Object;
			
			[_e] call _base.OnOpposingGroupInserted;
		};

		protected override method MissionObjective CreatePrimaryObjectiveForActing()
		{
			private _shortDescription = _mPrimaryTargetVehicle getVariable "ObjectiveText";
			private _longDescription = _shortDescription + ". The truck is located somewhere inside the area of Air Station Mike-26.";
			
			return [_shortDescription, _longDescription] new MissionObjective;
		};
		
		protected override method MissionObjective CreateSecondaryObjectiveForActing()
		{
			private _shortDescription = _mSecondaryTargetVehicle getVariable "ObjectiveText";
			private _longDescription = _shortDescription + ". The truck is located somewhere inside the area of Air Station Mike-26.";
			
			return [_shortDescription, _longDescription] new MissionObjective;
		};

		protected override method MissionObjective CreatePrimaryObjectiveForOpposing()
		{
			private _shortDescription = "Patrol Air Station Mike-26";
			private _longDescription = "Patrol the Air Station Mike-26 and keep it safe from intruders.";
			
			return [_shortDescription, _longDescription] new MissionObjective;
		};
		
		protected override method MissionObjective CreateSecondaryObjectiveForOpposing()
		{
			private _shortDescription = "Hunt down any intruders";
			private _longDescription = "Hunt down and kill all intruders.";
			
			return [_shortDescription, _longDescription] new MissionObjective;
		};
		
		protected override method ObjectiveState CheckPrimaryObjectiveStateActing()
		{
			if (!alive _mPrimaryTargetVehicle) then {
				return ObjectiveState.Succeeded;
			};
			
			return ObjectiveState.None;
		};
		
		protected override method ObjectiveState CheckSecondaryObjectiveStateActing()
		{
			if (!alive _mSecondaryTargetVehicle) then {
				return ObjectiveState.Succeeded;
			};
			
			return ObjectiveState.None;
		};
		
		protected override method ObjectiveState CheckPrimaryObjectiveStateOpposing()
		{
			private _vehiclesAlive = { alive _x } count _mAllVehicles == count _mAllVehicles;
			
			if (!_vehiclesAlive) then {
				return ObjectiveState.Failed;
			};
			
			private _missionHasEnded = _self.IsCancelling;
			private _allEnemiesDead = (call _self.CountUnitsAliveActing) == 0;
			
			if (_vehiclesAlive && (_allEnemiesDead || _missionHasEnded)) then {
				return ObjectiveState.Succeeded;
			};
			
			return ObjectiveState.None;
		};
		
		protected override method ObjectiveState CheckSecondaryObjectiveStateOpposing()
		{
			private _allEnemiesDead = (call _self.CountUnitsAliveActing) == 0;
			
			if (_allEnemiesDead && _self.MissionTimeElapsed >= 1) then {
				return ObjectiveState.Succeeded;
			};
			
			// If the acting side has extracted, the opposing side has failed to hunt them down.
			if (_self.ReturnObjectiveStateActing == ObjectiveState.Succeeded) then {
				return ObjectiveState.Failed;
			};
			
			return ObjectiveState.None;
		};
		
		private method Boolean IsAnyOpposingKilledByIntruder()
		{
			var _opposingActualSide = [MissionSide.Opposing] call _self.GetActualSide;
			
			{
				if (!alive _x) then
				{
					var _killedBySide = _x getVariable ["KilledBySide", _opposingActualSide];
					
					if (_killedBySide != _opposingActualSide) then {
						return true;;
					};
				};
			} foreach _mAllOpposingUnits as Object;
			
			return false;
		};
		
		private method CreateAndStartReinforcements()
		{
			var _reinforcementStartMarker = selectRandom ["m01o_ReinforcementStartMarker1", "m01o_ReinforcementStartMarker2"];
			_mProwler = ["PROWLER", _reinforcementStartMarker, MissionSide.Opposing] call _self.CreateSideVehicleOnMarker;
		
			var _rifleman = ["RIFLEMAN", getMarkerPos "m01o_ReinforcementStartMarker1", MissionSide.Opposing] call _self.CreateSideAiSoldier;
			_mReinforcementGroup = group _rifleman;
			
			_rifleman assignAsDriver _mProwler;
			_rifleman moveInDriver _mProwler;
			
			for "_i" from 1 to 4 do {
				_rifleman = ["RIFLEMAN", getMarkerPos "m01o_ReinforcementStartMarker1", MissionSide.Opposing, _mReinforcementGroup] call _self.CreateSideAiSoldier;
				_rifleman assignAsCargo _mProwler;
				_rifleman moveInCargo _mProwler;
			};
			
			var _waypoint = _mReinforcementGroup addWaypoint [getMarkerPos "m01o_startarea1", 40];
			_waypoint setWaypointSpeed "FULL";
			_waypoint setWaypointBehaviour "SAFE";
		};

		protected override method OnEachIteration()
		{
			if (!_mReinforcementsAlerted) then
			{
				private _allVehiclesAlive = { alive _x } count _mAllVehicles == count _mAllVehicles;
				
				if (!_allVehiclesAlive || { call _self.IsAnyOpposingKilledByIntruder }) then
				{
					call _self.CreateAndStartReinforcements;
					player sideChat "reinforcements on the way";
					_mReinforcementsAlerted = true;
				};
			};
			
			if (_mReinforcementsAlerted && !_mReinforcementsArrived) then
			{
				player sideChat str (_mProwler distance getMarkerPos "m01o_startarea1");
				player sideChat str (speed _mProwler);
			
				if (_mProwler distance getMarkerPos "m01o_startarea1" < 200 && speed _mProwler < 1) then
				{
					_mReinforcementGroup leaveVehicle _mProwler;
					[_mReinforcementGroup, "m01o_startarea1"] execVM "Engima\SearchPatrol\SearchPatrol.sqf";
					_mReinforcementsArrived = true;
				};
			};
		};
	};
};
